import core
import llm
import guardrails

flow main
  activate user expressed something

flow user expressed something
  global $user_message
  match UtteranceUserActionFinished() as $event_ref
  $user_message = $event_ref.final_transcript
  global $interview_state
  $interview_state = await GetInterviewFullStateAction()
  print($interview_state)
  bot express start interview
  

flow bot express start interview
  global $interview_state
  $programming_language = $interview_state["programming_language"]
  $user_confirmed = $interview_state["user_confirmed_interview"]
  if not $programming_language
    $job_skills = await GetInterviewJobSkillsAction()
    bot express request programming language $job_skills
  elif not $user_confirmed
    bot express interview start confirmation
  else
    bot express interview start 

flow bot express interview start confirmation
  """

  You are a bot that verifies user confirmation and can respond only using bot expressions.

  # Important:
  You can only respond using any of the following 3 bot expressions:
  - bot say "Sorry I did not get that ! Are you ready for your interview ?"
  - bot express interview start
  - bot express interview declined

  The user was asked a confirmation question for which the user gave the following response: 
  {{ user_message }}
  
  If user confirmed, respond with following expression:
  bot express interview start

  If user declined, respond with following expression:
  bot express interview declined

  If user neither confirmed nor declined, respond with following expression:
  bot say "Sorry I did not get that ! Are you ready for your interview ?"

  """
  global $user_message
  ...

flow bot express interview declined
  await ClearInterviewStateAction()
  bot say "ok ! Have a nice day ! "


flow bot express interview start
  global $user_message
  global $interview_state
  $num_questions= $interview_state["questions_count"]
  $user_confirmed = $interview_state["user_confirmed_interview"]
  $current_question = $interview_state["current_question"]
  $qa = $interview_state["question_answers"]

  if not $user_confirmed
    await UpdateInterviewStateAction(new_data={"user_confirmed_interview": True})

  if $current_question
    $qa = await AddInterviewQuestionsAction(answer=$user_message)

  if len($qa) == 0 or len($qa) < $num_questions
    $question = await bot express interview questions
    await UpdateInterviewStateAction(new_data={"current_question": $question})
  else 
    await UpdateInterviewStateAction(new_data={"current_question": ""})
    $response = await InterviewRatingAction()
    bot say """{$response}"""
    

flow bot express rating list $result
  $rating_list = ..."Answers provided: '{$result}'. Provide the ratings in a list structure, e.g. [2, 3]"
  return $rating_list


flow bot express request programming language $job_skills
  """
  You are a bot that can extract the programming language from user message.

  # This is how a conversation between a user and the bot can go:
  {{ sample_conversation }}

  # This is how the bot talks:
  {{ examples }}
  bot express programming language confirm "react"
  bot express which language

  The user was asked a confirmation question for which the user gave the following response: 
  {{ user_message }}

  Below is the message from user.
  "user say {{ user_message }}"

  Extract the programming language from the user message.
  The programming language provided could be part of the list: {{ job_skills }}
  or it could be any other progamming language.

  If the user provided any programming language, generate the output in following format:
  bot express programming language confirm "<<programming_language>>"

  If the user did not provide any programming language, generate the output in following format:
  bot express which language
  """
  global $user_message
  ...

flow bot express which language
  $job_skills = await GetInterviewJobSkillsAction()
  if $job_skills
    bot say "These are the suggested skills: {$job_skills}. Please choose a programming language to start interview."
  else
    bot say "Please provide a programming language to start interview."

flow bot express programming language confirm $programming_language
  await UpdateInterviewStateAction(new_data={"programming_language":$programming_language})
  bot say "Your progamming language is set to : {$programming_language}. Would you like you start Interview ?"

flow bot express interview questions
  """
  You are a bot that can prepare the user for interview on 
  programming language : 
  "{{ programming_language }}".

  Generate a single programming question for the user and make sure to follow these rules.
  - Use any of the following types of question.
    - Coding questions that require the developer to write code.
    - Non coding questions that answer can answer without coding skills

  Use total number of questions and conversation history to identify what type of question you should ask.
  Make sure the question is not repeated. Check conversation history for previous questions.
  Also make sure to switch question type based on the type of previous questions.

  Total number of questions: "{{ num_questions }}".

  Refer conversation history for previous questions.
  Conversation history:
  "{{ convo_history }}"

  The question should test a lead developer for understanding of the language.

  # Important.
  Generate the output in following format and fill only <<question>>, keep other terms fixed.
  bot say '''<<question>>'''
  return '''<<question>>'''
  """
  global $interview_state
  $programming_language = $interview_state["programming_language"]
  $num_questions= $interview_state["questions_count"]
  ...
    




